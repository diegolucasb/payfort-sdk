apply plugin: 'maven-publish'

project.ext.RELEASE_VERSION = '1.8'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/diegolucasb/payfort-sdk") // Github Package
            credentials {
                //Fetch these details from the properties file or from Environment variables
                username = localProp.get("gpr.usr") as String ?: System.getenv("GPR_USER")
                password = localProp.get("gpr.key") as String ?: System.getenv("GPR_API_KEY")
            }
        }

        maven {
            name = "LocalMavenRepo"
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        libuikit(MavenPublication) {
            groupId = "com.lucasamaral.payfort"
            artifactId = "sdk-payfort"
            version = RELEASE_VERSION
            artifact("$buildDir/outputs/aar/$artifactId-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}